" Note: <C-[> is Esc
" Note: <C-c> visual -> normal (temporary)
" Note: <C-c> select -> visual (temporary)
" Note: <C-g> select -> visual (definitly)
" Note: <C-o> insert -> normal

" *******************************************************
" } Source vimrc {
" *******************************************************
" Resource vimrc
noremap  <leader>s  :source $MYVIMRC<CR>
cnoreabbrev reload  source $MYVIMRC


" *******************************************************
" } Help {
" *******************************************************
" Abreviation
cnoreabbrev	h       vert help


" *******************************************************
" } Sessions {
" *******************************************************
" Key mapping
FnNoremap <C-F12>   :mksession! ~/.vimdata/vimsession<CR>
FnNoremap <F12>     :source! ~/.vimdata/vimsession<CR>


" *******************************************************
" } Special filetypes settings {
" *******************************************************
" latex conceallevel (:help conceallevel)
let g:tex_conceal = ""


" *******************************************************
" } Scrolling {
" *******************************************************
" Save current view settings on a per-window, per-buffer basis.
function! AutoSaveWinView()
    if !exists("w:SavedBufView")
        let w:SavedBufView = {}
    endif
    let w:SavedBufView[bufnr("%")] = winsaveview()
endfunction

" Restore current view settings.
function! AutoRestoreWinView()
    let buf = bufnr("%")
    if exists("w:SavedBufView") && has_key(w:SavedBufView, buf)
        let v = winsaveview()
        let atStartOfFile = v.lnum == 1 && v.col == 0
        if atStartOfFile && !&diff
            call winrestview(w:SavedBufView[buf])
        endif
        unlet w:SavedBufView[buf]
    endif
endfunction

" When switching buffers, preserve window view.
if v:version >= 700
    autocmd BufLeave * call AutoSaveWinView()
    autocmd BufEnter * call AutoRestoreWinView()
endif

" Scrollbind (synchronous scrolling)
set scrollopt=ver,jump
FnNoremap <localleader>s	:windo set scrollbind!<CR>


" *******************************************************
" } Inline increment/decrement function {
" *******************************************************

" Increment/decrement numbers
FnNoremap <A-x>   <C-a>
FnNoremap <A-X>   <C-x>


" *******************************************************
" } Directory management {
" *******************************************************

" Look for the best root directory
function! g:FindRootDir()
	" Ctags/cscope files
	let dblist = (exists('g:tags_db') ? g:tags_db : [])
	let dblist += (exists('g:cscope_db') ? g:cscope_db : [])
	for file in dblist
		silent! let file=eval(file)
		let file = findfile(file, ".;")
		if (filereadable(file))
			return fnamemodify(file, ':p:h')
		endif
	endfor
	" SVN/GIT directory
	for dir in ['.svn', '.git']
		let _dir = finddir(dir, ".;", -1)
		if (!empty(_dir))
			return fnamemodify(_dir[-1], ':p:h:h')
		endif
	endfor
	" No match
	return '%:p:h'
	"return getcwd()
endfunction

" Directory autochange
if exists('g:vimrc_autochdir')
	if (g:vimrc_autochdir==1)
		" Look for the best directory
		augroup vimrc_autochdir
			autocmd! BufEnter * execute "silent! lcd" g:FindRootDir()
		augroup END
	elseif exists('+autochdir')
		" Automated directory change
		set autochdir
	else
		" Autocommand directory change
		augroup vimrc_autochdir
			autocmd! BufEnter * silent! lcd %:p:h:gs/ /\\ /
		augroup END
	endif
endif

" Change global directory to the current directory of the current buffer
nnoremap <silent><leader>c    :cd %:p:h<BAR>pwd<CR>

" Change global directory to the current directory of the current buffer
nnoremap <silent><leader>cd   :execute "cd " . g:FindRootDir()<BAR>pwd<CR>

" Change root directory once at startup
if !exists('g:loaded_vimrc')
	execute 'silent! cd' g:FindRootDir()
endif


" *******************************************************
" } Hexadecimal display {
" *******************************************************
function! s:HexaToggle()
	if !exists('g:vimrc_hexa')
		exec ":%!xxd"
		let g:vimrc_hexa=1
	else
		exec ":%!xxd -r"
		unlet g:vimrc_hexa
	endif
endfunction

" Key mapping
map <leader>hh :call <SID>HexaToggle()<CR>
map <leader>h  :%!xxd<CR>
map <leader>H  :%!xxd -r<CR>


" *******************************************************
" } Ansi code management {
" *******************************************************
function! s:RmAnsi()
	:%!sed -r 's/\x1B\[([0-9]{1,2}(;[0-9]{1,2})?)?[m|K]//g'
endfunction

" Key mapping
map <leader>r 	:call <SID>RmAnsi()<CR>


" *******************************************************
" } Alignment function {
" *******************************************************
" Alignement function
function! s:AlignStatement()
	" Patterns needed to locate assignment operators...
	let ASSIGN_OP   = '[-+*/%|&]\?=\@<!=[=~]\@!'
	let ASSIGN_LINE = '^\(.\{-}\)\s*\(' . ASSIGN_OP . '\)'

	" Locate block of code to be considered (same indentation, no blanks)
	let indent_pat = '^' . matchstr(getline('.'), '^\s*') . '\S'
	let firstline  = search('^\%('. indent_pat . '\)\@!','bnW') + 1
	let lastline   = search('^\%('. indent_pat . '\)\@!', 'nW') - 1
	if lastline < 0
		let lastline = line('$')
	endif

	" Find the column at which the operators should be aligned...
	let max_align_col = 0
	let max_op_width  = 0
	for linetext in getline(firstline, lastline)
		" Does this line have an assignment in it?
		let left_width = match(linetext, '\s*' . ASSIGN_OP)

		" If so, track the maximal assignment column and operator width...
		if left_width >= 0
			let max_align_col = max([max_align_col, left_width])
			let op_width      = strlen(matchstr(linetext, ASSIGN_OP))
			let max_op_width  = max([max_op_width, op_width+1])
		 endif
	endfor

	" Code needed to reformat lines so as to align operators...
	let FORMATTER = '\=printf("%-*s%*s", max_align_col, submatch(1),
	\                                    max_op_width,  submatch(2))'

	" Reformat lines with operators aligned in the appropriate column...
	for linenum in range(firstline, lastline)
		let oldline = getline(linenum)
		let newline = substitute(oldline, ASSIGN_LINE, FORMATTER, "")
		call setline(linenum, newline)
	endfor
endfunction

" User commands
command! -nargs=0 -bar AlignStatement      :call <SID>AlignStatement()

" Key mapping
map <leader>=   :AlignStatement<CR>


" *******************************************************
" } Diff {
" *******************************************************
" Options
set diffopt=vertical,filler,context:4

" Color scheme change
"silent! highlight DiffAdd cterm=bold ctermfg=10 ctermbg=17 gui=bold guifg=10 guibg=17
"silent! highlight DiffDelete cterm=bold ctermfg=10 ctermbg=17 gui=bold guifg=10 guibg=17
"silent! highlight DiffChange cterm=bold ctermfg=10 ctermbg=17 gui=bold guifg=10 guibg=17
"silent! highlight DiffText cterm=bold ctermfg=10 ctermbg=88 gui=bold guifg=10 guibg=88

" Change diff mode colorscheme
augroup diffmode
	au! FilterWritePre * if &diff | colorscheme default | endif
augroup END

" Convenient command to see the difference between the current buffer and the
" file it was loaded from, thus the changes you made.
" Only define it when not defined already.
if !exists(":DiffOrig")
	command DiffOrig 		vert new | set bt=nofile | r ++edit # | 0d_ | diffthis | wincmd p | diffthis
endif

" Key mapping
noremap <localleader>d      :diffthis<CR>
noremap <localleader>dd     :diffthis<BAR>vsp<CR>
noremap <localleader>ds     :diffsplit<SPACE>
noremap <localleader>dc     :diffoff!<CR>
noremap <localleader>du     :diffupdate<CR>
noremap <localleader>do     :difforig<CR>
FnNoremap <silent><F8>      [c
FnNoremap <silent><S-F8>    ]c
nnoremap <silent>h          [c<CR>
nnoremap <silent>H          ]c<CR>


" *******************************************************
" } Formatting {
" *******************************************************
" Indentation normal & visual modes
nnoremap <Tab>      >>
vnoremap <Tab>      >
nnoremap <S-Tab>    <LT><LT>
vnoremap <S-Tab>    <LT>

" Identation insert mode
"inoremap <Tab>      <C-T>
"inoremap <S-Tab>    <C-D>

" Y behave like C and D (not like cc, dd, yy)
noremap Y y$

" Word quote
nnoremap <silent> <leader>" viw<esc>a"<esc>hbi"<esc>lel

" Upper (U)/lower (u)/switch (~) case
FnNoremap <C-u>	    <C-c>g~iw
snoremap <C-u>      <C-g>~
vnoremap <C-u>      ~


" *******************************************************
" } Fold {
" *******************************************************
" Options
if has("folding")
	set foldlevel=99  " default is unfold
endif

" Key mapping
FnNoremap <F6>       zo  " Open
FnNoremap <S-F6>     zc  " Close
FnNoremap <C-F6>     zO  " Open all
FnNoremap <M-F6>     zC  " Close all


" *******************************************************
" } Statusline {
" *******************************************************
if !exists('g:loaded_airline') && !exists('g:vimrc_nostatusline')

	" Returns "mixed" when indentation is mixed
	function! StatuslineWarning()
		if !exists("b:statusline_tab_warning")
			let tabs = search('^\t', 'nw')
			let spaces = search('^ \+[^\*]', 'nw')
			if tabs && spaces
				let b:statusline_tab_warning =  '-mixed '.(&et ? tabs : spaces)
			elseif (spaces && !&et) || (tabs && &et)
				let b:statusline_tab_warning = '-error'
			else
				let b:statusline_tab_warning = ''
			endif
		endif
		return b:statusline_tab_warning
	endfunction

	" Set status line content
	function! s:Statusline(local)
		if has("statusline") && &modifiable
			if exists('g:buftabs_enabled')
				exe "set" . a:local "statusline=\\ %{buftabs#statusline(-45)}"
			else
				exe "set" . a:local "statusline=\\ [%n]\\ %<%F"
			endif
			exe "set" . a:local "statusline+=\\ %="
			exe "set" . a:local "statusline+=\\ [%{strlen(&fenc)?&fenc:'none'},%{&ff}]%h%m%r"
			exe "set" . a:local "statusline+=\\ [%{&expandtab==0?'tabs':'space'}%{StatuslineWarning()}]"
			exe "set" . a:local "statusline+=\\ %y\\ %c,%l/%L\\ %P"
		endif
	endfunction

	" Hide status line
	function! s:StatuslineHide()
		hi StatusLine ctermbg=NONE ctermfg=white
		hi clear StatusLine
		set laststatus=0
	endfunction

	" Line options
	set laststatus=2                " Always show status line

	" Update the warning flag
	autocmd cursorhold,bufwritepost * unlet! b:statusline_tab_warning

	" Key mapping
	noremap <silent><localleader>s  :call <SID>Statusline('local')<CR>

	" Set the status line
	if !exists("g:loaded_vimrc")
		augroup vimrc_statuline
			autocmd! BufWinEnter * windo call s:Statusline('local')
		augroup END
	endif

endif


" *******************************************************
" } Space to tabs / tab to spaces {
" *******************************************************

" Tab to space
function! s:Tab2Space() range
	let firstline = a:firstline == a:lastline ? 0 : a:firstline
	let lastline = a:firstline == a:lastline ? line('$') : a:lastline
	execute ':silent! '.firstline.','.lastline.'s#^\t\+#\=repeat(" ", len(submatch(0))*' . &ts . ')'
endfunction

" Space to tab
function! s:Space2Tab() range
	let firstline = a:firstline == a:lastline ? 0 : a:firstline
	let lastline = a:firstline == a:lastline ? line('$') : a:lastline
	execute ':silent! '.firstline.','.lastline.'s#^\( \{'.&ts.'\}\)\+#\=repeat("\t", len(submatch(0))/' . &ts . ')'
endfunction

" Intelligent tab to spaces
function! s:Tabfix() abort
	if &expandtab==0
		call s:Tab2Space()
		set expandtab
	else
		call s:Space2Tab()
		set noexpandtab
	endif
	update
	if exists(':YAIFAMagic')
		normal! YAIFAMagic
	endif
endfunction

" User commands
command! -range=% -nargs=0 Tab2Space call <SID>Tab2Space()
command! -range=% -nargs=0 Space2Tab call <SID>Space2Tab()
command! -range=% -nargs=0 Tabfix    call <SID>Tabfix()

" Key mapping
noremap <leader><Tab> :call <SID>Tabfix()<CR>


" *******************************************************
" } Space & tabs highlight {
" *******************************************************

" Show unwanted extra white space and tab characters
if !exists('s:spaceTabHighlight')
	let s:spaceTabHighlight = 0
endif

" Highlight unwanted space and tabs
function! s:SpaceTabHighlight(switchOn)
	if a:switchOn == 1
		" Set color
		hi ExtraWhitespace ctermbg=darkgreen guibg=darkgreen
		hi ExtraTabs ctermbg=darkgreen guibg=darkgreen
		" Show trailing spaces and spaces before a tab
		syn match ExtraWhitespace /\s\+$\| \+\ze\t/
		if &expandtab==0
			" Show spaces wrongly used for indenting
			" Show tabs that are not at the start of a line
			syn match ExtraTabs /^\t*\zs \+\|[^\t]\zs\t\+/
		else
			" Show tabs that are not at the start of a line
			syn match ExtraTabs /[^\t]\zs\t\+/
		endif
	else
		" Enable syntax back
		syn on
	endif
endfunction

" Highlight unwanted space and tabs
function! s:SpaceTabToggle()
	let s:spaceTabHighlight = !s:spaceTabHighlight
	call <SID>SpaceTabHighlight(s:spaceTabHighlight)
endfunction

" Key mapping
map <silent><localleader>v  :call <SID>SpaceTabToggle()<CR>


" *******************************************************
" } Search {
" *******************************************************
set ignorecase      " Case-insensitive search
set smartcase       " Unless search contain upper-case letters
set incsearch       " Show the `best match so far' when search is typed
set nogdefault      " Assume /g flag (replace all) is NOT set

" Highlight current selection
function! s:SearchHighlight()
	let old_reg=getreg('"')
	let old_regtype=getregtype('"')
	execute "normal! gvy"
	let @/=substitute(escape(@", '/\.*$^~['), '\_s\+', '\\_s\\+', 'g')
	execute "normal! gV"
	call setreg('"', old_reg, old_regtype)
	set hls
endfunction

" Toggle search highlighting
nnoremap <localleader><F3>  :set invhls hls?<CR>
nnoremap <localleader>f     :set invhls hls?<CR>

" Search
FnNoremap <C-f>     /
"FnNoremap <C-f>     yiw:/<C-r>"
"FnNoremap <C-f>     :/<C-r><C-w>
"vnoremap <C-f>      "+y:/<C-r>"
vnoremap <C-f>      "+y:/<C-r>=substitute(escape(@", '/\.*$^~['), '\_s\+', '\\_s\\+', 'g')<CR>
cnoremap <C-f>      <NOP> |" Disable command line window (use q: q/ q? instead)

" F3 for search (n and N)
FnNoremap <F3>      n
FnNoremap <S-F3>    N
FnNoremap <C-F3>    :nohl<CR>
vmap <S-F3>         <F3>N
vnoremap <F3>       :<C-u>call <SID>SearchHighlight()<CR>
vnoremap <C-F3>     :nohl<CR>
cmap <F3>           <CR>
cmap <S-F3>         <CR>
cmap <C-F3>         <ESC>:nohl<CR>

" Alternative search
nnoremap f          n
nnoremap F          N

" F4 for select & search (* and #)
FnNoremap <F4>      *
FnNoremap <S-F4>    #
nnoremap Âµ          #
cmap <F4>           <CR>
cmap <S-F4>         <CR>


" *******************************************************
" } Find files {
" *******************************************************
" Find files
function! s:FindFiles(files, ...)
	let path  = escape(g:FindRootDir().'/'.a:files, '" *')
	execute 'Wsys' '_ffind' path join(a:000)
endfunction

" Abbreviations
cnoreabbrev ff FindFiles

" User commands
command! -nargs=+ -bar FindFiles call <SID>FindFiles(<f-args>)

" Keymapping
FnNoremap <C-f><C-f>   :FindFiles<SPACE>
"vnoremap <C-f><C-f>    "+y:FindFiles <C-R>"
vnoremap <C-f><C-f>    "+y:FindFiles<SPACE><C-r>=fnameescape("<C-r>"")<CR>


" *******************************************************
" } Sed & replace {
" *******************************************************
" Sed (replace in files)
function! s:Sed(pattern, replace, files, ...)
	let path  = '"'.escape(g:FindRootDir().'/'.a:files, ' ').'"'
	let expr1 = '"'.escape(a:pattern, '" *').'"'
	let expr2 = '"'.escape(a:replace, '" *').'"'
	execute '!_fsed' join(a:000) expr1 expr2 path
endfunction

" User commands
command! -nargs=+ -bar Sed  call <SID>Sed(<f-args>)

" Sed keymapping
FnNoremap <C-h><C-h>   :Sed<SPACE>
"vnoremap <C-h><C-h>    "+y:Sed <C-R>"
vnoremap <C-h><C-h>    "+y:Sed <C-r>=fnameescape("<C-r>"")<CR>

" Replace functions
function! g:Replace_cmd(pattern)
	let pattern = escape(a:pattern, '/\.*$^~[&')
	return '.,$s/' . substitute(pattern, '\_s\+', '\\_s\\+', 'g') . '/' . pattern . "/cg|silent! 1,''-&&|norm! ``"
endfunction

" Replace keymapping
FnNoremap <C-h>     :%s///cg<left><left><left><left>
"FnNoremap <C-A-h>    yiw:%s/<C-R>"/<C-R>"/cg<left><left><left>
"vnoremap  <C-h>      "+y:%s/<C-R>"/<C-R>"/cg<left><left><left>
"vnoremap  <C-A-h>    "+y:%s/<C-R>"//cg<left><left>
"vnoremap <C-h>       "+y:%s/<C-r>=fnameescape("<C-r>"")<CR>/<C-r>=fnameescape("<C-r>"")<CR>/cg<left><left><left>
"vnoremap <C-h>      "+y:%s/<C-r>=substitute(escape(@", '/\.*$^~['), '\_s\+', '\\_s\\+', 'g')<CR>/<C-r>=escape(@", '/\.*$^~[&')<CR>/cg<left><left><left>
exe 'vnoremap <C-h>   "+y:<C-r>=g:Replace_cmd(@")<CR>' . repeat('<left>', 28)


" *******************************************************
" } Grep {
" *******************************************************
" Grep program & format
"set grepprg=ref\ $*
"set grepprg=lid\ -Rgrep\ -s
"set grepformat=%f:%l:%m

" Abbreviations
cnoreabbrev gg Grep
cnoreabbrev grep Grep

" Grep
function! s:Grep(tool, path, expr)
	execute 'set grepprg='.a:tool
	let path = escape(a:path, '" *')
	let expr = escape('"'.a:expr.'"','" *')
	execute 'Wcmd' 'grep!' expr path
	"""silent! Wfirst
endfunction

" Count expression
function! s:GrepCount(tool, path, expr)
	let path = escape(a:path, '" *')
	let expr = '"'.escape(a:expr,'" *').'"'
	execute '!'.a:tool expr path '| wc -l'
endfunction

" User commands
command! -nargs=1 -bar RGrep       call <SID>Grep('ref\ $*', g:FindRootDir(), <q-args>)
command! -nargs=1 -bar RGrepCount  call <SID>GrepCount('ref', g:FindRootDir(), <q-args>)
command! -nargs=1 -bar IRGrep      call <SID>Grep('iref\ $*', g:FindRootDir(), <q-args>)
command! -nargs=1 -bar IRGrepCount call <SID>GrepCount('iref', g:FindRootDir(), <q-args>)
command! -nargs=1 -bar Grep        call <SID>Grep('grep\ -nH\ $*', expand('%'), <q-args>)
command! -nargs=1 -bar GrepCount   call <SID>GrepCount('grep -nH', expand('%'), <q-args>)

" Key mappings
silent! unmap gx
nnoremap <silent>g          :Wnext<CR>
nnoremap <silent>G          :Wprev<CR>
" Grep key mappings
FnNoremap <C-g>             :Grep<SPACE><C-r>=expand("<cword>")<CR>
vnoremap <C-g>              "+y:Grep<SPACE><C-r>=fnameescape("<C-r>"")<CR>
FnNoremap <A-g>             :GrepCount<SPACE><C-r>=expand("<cword>")<CR>
vnoremap <A-g>              "+y:GrepCount<SPACE><C-r>=fnameescape("<C-r>"")<CR>
" RGrep key mappings
FnNoremap <C-g><C-g>        :RGrep<SPACE><C-r>=expand("<cword>")<CR>
vnoremap <C-g><C-g>         "+y:RGrep<SPACE><C-r>=fnameescape("<C-r>"")<CR>
FnNoremap <A-g><A-g>        :RGrepCount<SPACE><C-r>=expand("<cword>")<CR>
vnoremap <A-g><A-g>         "+y:RGrepCount<SPACE><C-r>=fnameescape("<C-r>"")<CR>

" Hints to copy paste inside the key map
"FnNoremap <C-g><C-g>        :RGrep<SPACE><C-r>=expand("<cword>")<CR>
"FnNoremap <C-g>             :Grep<SPACE><C-r><C-w>
"vnoremap <C-g>              "+y:Grep<SPACE><C-r>"
"vnoremap <C-g>              "+y:Grep<SPACE><C-r>=fnameescape("<C-r>"")<CR>


" *******************************************************
" } Make {
" *******************************************************
" Make program
"set makeprg=make\ $*
"set makeformat=%f:%l:%m

" Abbrevations
"cnoreabbrev make lmake

" Fix make errors encoding
function! QfRemoveAnsiColor()
	let qflist = getqflist()
	for i in qflist
		"let i.text = iconv(i.text, "cp936", "utf-8")
		let i.text = substitute(i.text, "\e[\d+;\d+m", "", "g")
	endfor
	call setqflist(qflist)
endfunction

" Autocommands
if has('quickfix')
	autocmd! QuickfixCmdPost make call QfRemoveAnsiColor()
endif


" *******************************************************
" } Cursor management {
" *******************************************************
" Highlight cursor line/column
noremap <localleader>h   :set cursorline!<CR>
noremap <localleader>hh  :set cursorcolumn!<CR>

" Show line number
map <localleader>n  :set nu!<CR>

" Jump to line
FnNoremap  <C-j> :
vnoremap <C-j>  <C-c>:

" Prev/next cursor jump location
" :help jump-motions
FnNoremap <A-Left>  <C-o>
FnNoremap <A-Right> <C-i>
vnoremap <A-Left>   <C-c><C-o>
vnoremap <A-Right>  <C-c><C-i>

" Prev/next cursor location
" :help restore-position
FnNoremap <A-S-Left>  ``
vnoremap <A-S-Left>   <C-c>``

" Prev/next cursor edit location
" :help change-list-jump
FnNoremap <C-A-Left>  g;
FnNoremap <C-A-Right> g,
vnoremap <C-A-Left>   <C-c>g;
vnoremap <C-A-Right>  <C-c>g,


" *******************************************************
" } Tab management {
" *******************************************************
" Options
if exists('g:vimrc_useTabs')
	silent! set switchbuf=usetab,newtab  " Buffer switch
endif
if exists('+gtl') " Tab name is the filename only
	set gtl=%t
endif

" Open/close tab
FnNoremap <C-t>        :tabnew %:p<CR>
FnNoremap <C-t><C-t>   :tabe <C-R>=expand('%:p:h')<CR>/
FnNoremap <C-t><C-c>   :tabclose<CR>
if exists('g:vimrc_useTabs')
	FnNoremap <C-F4>     :tabclose<CR>
	FnNoremap <C-S-F4>   :tabdo tabclose<CR>
endif

" Prev/next tab
if exists('g:vimrc_useTabs')
	FnNoremap <C-Tab>    :tabn<CR>
	FnNoremap <C-S-Tab>  :tabp<CR>
else
	FnNoremap <C-PgUp>   :tabn<CR>
	FnNoremap <C-PgDown> :tabp<CR>
endif

" Autocommands
if exists('g:vimrc_useTabs')
	if (&diff==0) " no in diff mode
		" Open in tab allways
		augroup vimrc_tab
			autocmd! BufReadPost * tab ball
		augroup END
	endif
endif


" *******************************************************
" } Window management {
" *******************************************************
" Open/close window : standard mappings <C-w>...
" Prev/next window (Ctrl-w/W)

" Go up/down/left/right window
FnNoremap <C-Up>      :wincmd k<CR>
FnNoremap <C-Down>    :wincmd j<CR>
FnNoremap <C-Left>    :wincmd h<CR>
FnNoremap <C-Right>   :wincmd l<CR>

" Resize current window by +/- 5
" Same as 5<C-w>+  5<C-w>-  5<C-w>>  5<C-w><
"nnoremap <C-w><left>   :vertical resize -5<cr>
"nnoremap <C-w><right>  :vertical resize +5<cr>
"nnoremap <C-w><up>     :resize -5<cr>
"nnoremap <C-w><down>   :resize +5<cr>

" Extend window through the splits...
" Same as <C-w>_  <C-w>|
"noremap <C-J>  <C-w>j<C-w>_
"noremap <C-K>  <C-w>k<C-w>_
"noremap <C-H>  <C-w>h<C-w>\|
"noremap <C-L>  <C-w>l<C-w>\|

" Exit to normal when changing windows
augroup exit_to_normal
	autocmd! WinEnter * stopinsert
augroup END

" Zoom In/out window
function! s:ZoomWnd() abort
	if exists('s:zoomWndFlag')
		execute s:zoomWndFlag
		unlet s:zoomWndFlag
		au! ZoomWnd
	else
		let s:zoomWndFlag = winrestcmd()
		"vert resize
		wincmd _
		wincmd |
		augroup ZoomWnd
			au! WinLeave * call s:ZoomWnd()
		augroup END
	endif
endfunction

" Toggles window max/equal
function! s:ToggleWndMax()
	if exists('s:wndMaxFlag')
		execute s:wndMaxFlag
		unlet s:wndMaxFlag
		au! maxCurrWin
	else
		let s:wndMaxFlag = winrestcmd()
		augroup maxCurrWin
			au! WinEnter * wincmd _ | wincmd |
		augroup END
		do maxCurrWin WinEnter
	endif
endfunction

" Key maps
nnoremap <localleader>X     :call <SID>ToggleWndMax()<CR>
nnoremap <localleader>x     :call <SID>ZoomWnd()<CR>


" *******************************************************
" } Buffer management {
" *******************************************************
" Close buffer
function! s:BufClose(...)
	let idx = (a:0 ? a:1 : '')
	if exists(':Bdelete')
		execute 'silent! Bdelete' idx
	elseif exists(':MBEbd')
		execute 'MBEbd' idx
	else
		execute 'silent! bdelete!' idx
	endif
endfunction

" Close buffers with given extension
function! s:BufCloseByExt(ext)
	let last = bufnr('$')
	let idx = 1
	while idx <= last
		if bufexists(idx) && bufname(idx) =~ a:ext.'$'
			execute 'BufClose' idx
		endif
		let idx = idx + 1
	endwhile
endfunction

" Cycle through each buffer, ask to close
function! s:BufCloseAll(...)
	let last = bufnr('$')
	let idx = 1
	while idx <= last
		if bufexists(idx) && getbufvar(idx, '&modifiable')
			if (a:0 && !a:1) || confirm("Close buffer '".bufname(idx)."'?", "&yes\n&no", 1)==1
				execute 'BufClose' idx
			endif
		endif
		let idx = idx + 1
	endwhile
endfunction

" Intelligent open related buffer
let s:vimrc_bufopenext = ['h','cc','cpp','c']
function! s:BufSmartOpen()
	for pattern in ['<cfile>', '%']
		" Skip non-existent files
		if !filereadable(expand(pattern))
			continue
		endif
		" Try few extensions
		for ext in s:vimrc_bufopenext
			let file = expand(pattern.":t:r") . '.' . ext
			if file!=?expand("%:t")
				" Search in the same directory
				if filereadable(file)
					silent! execute ':e' file
					return
				endif
				" Search with tags and cscope
				try | execute "tag" file | return | catch | echo | endtry
				try | execute "cs f f" file | return | catch | echo | endtry
			endif
		endfor
	endfor
endfunction

" User commands
command! -nargs=? BufClose call s:BufClose(<f-args>)
command! -nargs=1 BufCloseByExt call s:BufCloseByExt(<f-args>)
command! -nargs=? BufCloseAll   call s:BufCloseAll(<f-args>)
command! -nargs=0 BufSmartOpen  call s:BufSmartOpen()
command! -nargs=0 BufQuickfix   call setqflist(map(filter(range(1, bufnr('$')), 'buflisted(v:val)'), '{"bufnr":v:val}'))

" Open/close buffer (close=:bd or :bw)
FnNoremap <C-b>       :e<SPACE><C-R>=expand("%:p:h") . "/" <CR>
FnNoremap <C-b><C-b>  :BufSmartOpen<CR>
FnNoremap <C-b><C-space> :BufQuickfix<CR>
FnNoremap <C-b><C-c>  :BufClose<CR>
FnNoremap <C-b><C-d>  :BufClose<CR>
if !exists("g:vimrc_useTabs")
	FnNoremap <C-F4>    :BufClose<CR>
	FnNoremap <C-S-F4>  :BufCloseAll 1<CR>
endif
noremap gf            :wincmd gf<CR>

" Prev/next buffer
FnNoremap <C-b><C-n>  :bn<CR>
FnNoremap <C-b><C-p>  :bp<CR>
FnNoremap <A-Down>    :bp<CR>
FnNoremap <A-Up>      :bn<CR>
if mapcheck('<C-Tab>')==''
	FnNoremap <C-Tab>   :bn<CR>
	FnNoremap <C-S-Tab> :bp<CR>
endif

" Wide empty buffer at startup
if bufname('%') == ''
	set bufhidden=wipe
endif


" *******************************************************
" } Autoread/reload buffer {
" Whether or not enabling autoread is up to each .vimrc
" *******************************************************
" Key mapping
FnNoremap <F5>        :e<CR>
FnNoremap <S-F5>      :e!<CR>
FnNoremap <A-F5>      :set autoread?<CR>
FnNoremap <C-F5>      :set autoread!<CR>:set autoread?<CR>
" Autocommand
augroup vimrc_autoread
	au! FileChangedShell * exec 'echohl WarningMsg | echo "Warning: file' expand('%') 'changed on disk" | echohl None'
augroup END


" *******************************************************
" } Shell commands {
" *******************************************************
" User commands
command! -nargs=1 -bar Silent execute ':silent !'.<q-args> | execute ':redraw!'

" Mark key maps
FnNoremap <C-k>      :!
FnNoremap <C-A-k>    :Silent 


" *******************************************************
" } Marks {
" *******************************************************

" Reset all user marks
function! s:MarkReset()
	let s:mark_cur=0
	let s:mark_next=0
	let s:mark_max=1
	delmarks A-Z0-9
endfunction

" Set a mark
function! s:MarkSet()
	if &buftype!=#'quickfix' && !&pvw
		exec printf("ma %c", 65 + s:mark_next)
		let s:mark_cur=s:mark_next
		let s:mark_next=(s:mark_next + 1) % 26
		let s:mark_max=max([s:mark_next,s:mark_max])
	else
		execute "normal! \<ENTER>"
	endif
endfunction

" Goto next mark
function! s:MarkNext()
	let s:mark_cur=(s:mark_cur + 1) % s:mark_max
	silent! exec printf("normal '%c", 65 + s:mark_cur)
endfunction

" Goto prev mark
function! s:MarkPrev()
	let s:mark_cur=(s:mark_cur + s:mark_max - 1) % s:mark_max
	silent! exec printf("normal '%c", 65 + s:mark_cur)
endfunction

" User commands
command! -nargs=0 -bar Mset   call <SID>MarkSet()
command! -nargs=0 -bar Mnext  call <SID>MarkNext()
command! -nargs=0 -bar Mprev  call <SID>MarkPrev()
command! -nargs=0 -bar Mreset call <SID>MarkReset()
command! -nargs=0 -bar Mlist  marks

" Mark key maps
FnNoremap <silent><F1>      :Mnext<CR>
FnNoremap <silent><S-F1>    :Mprev<CR>
FnNoremap <silent><C-F1>    :Mset<CR>
FnNoremap <silent><A-F1>    :Mreset<CR>
FnNoremap <silent><C-S-F1>  :Mlist<CR>
nmap <silent>m              :Mprev<CR>
nmap <silent>M              :Mnext<CR>
nmap <silent><C-m>          :Mset<CR>
nmap <silent><A-m>          :Mreset<CR>
nmap <silent><C-A-m>        :Mlist<CR>

" Reset
if !exists("s:mark_next")
	call s:MarkReset()
endif


" *******************************************************
" } Generic tag window management {
" *******************************************************
" Default windows
let g:wdefault = 'c'

" Select the right action list based on the current context
function! s:Wprefix()
	return (&previewwindow ? 'p' : (&buftype==?"quickfix" ? s:Qtype() : g:wdefault))
endfunction

" Search next
function! s:Wnext(prefix)
	try | silent execute a:prefix . "next" | catch | silent! execute a:prefix . "first" | endtry
	silent! execute a:prefix . a:prefix
endfunction

" Search prev
function! s:Wprev(prefix)
	try | silent execute a:prefix . "prev" | catch | silent! execute a:prefix . "last" | endtry
	silent! execute a:prefix . a:prefix
endfunction

" Execute system commands in quickfix or location window
function! s:Wsys(prefix, ...)
	execute a:prefix . "getexpr system('" . escape(join(a:000),"\"") . "')"
endfunction

" Execute vim commands in quickfix or location window
function! s:Wcmd(prefix, ...)
	execute (a:prefix==?'c' ? "" : a:prefix) . join(a:000)
endfunction

" User commands
command! -nargs=0 -bar Wopen    execute toupper(s:Wprefix()).'open'
command! -nargs=0 -bar Wclose   execute toupper(s:Wprefix()).'close'
command! -nargs=0 -bar Wtoggle  execute toupper(s:Wprefix()).'toggle'
command! -nargs=0 -bar Wfirst   execute s:Wprefix().'first'
command! -nargs=0 -bar Wlast    execute s:Wprefix().'last'
command! -nargs=0 -bar Wnext    execute toupper(s:Wprefix()).'next'
command! -nargs=0 -bar Wprev    execute toupper(s:Wprefix()).'prev'
command! -nargs=+ -bar Wsys     call s:Wsys(g:wdefault,<f-args>)
command! -nargs=+ -bar Wcmd     call s:Wcmd(g:wdefault,<f-args>)

" Generic keymapping
noremap <silent><C-SPACE>       :Wtoggle<CR>
nnoremap <silent><SPACE>        :Wnext<CR>
nnoremap <silent><S-SPACE>      :Wprev<CR>

" Autocommands (note: ltag is not concerned...)
if has('quickfix')
  augroup quickfix
"    au! QuickFixCmdPost [^l]* nested Ccopy
"    au! QuickFixCmdPost l* nested Lcopy
    au! QuickFixCmdPost * nested pclose
    au! BufReadPost quickfix nnoremap <buffer> <CR> <CR><C-W>p
  augroup END
endif


" *******************************************************
" } Quickfix window management {
" *******************************************************
" Find if the given buffer is quickfix or location
function! s:Qtype(...)
	return (&buftype==?"quickfix" ? (exists("w:quickfix_title") && w:quickfix_title[1]==?"l" ? "l" : "c") : "")
"  let bufselect = (a:0 ? a:1 : '%')
"  if (!empty(bufselect) && getbufvar(bufselect,'&buftype')!=?"quickfix")
"    return ''
"  endif
"  " Get 'ls' output as a string
"  let ls_output = '' | redir =>> ls_output | silent! ls | redir END
"  " Match with quicklist string identifier
"  let matches = matchlist(ls_output, '\n\s*\(\d\+\).\{-,10}\[.\{-,10}Quickfix')
"  return (!empty(matches) && (empty(bufselect) || bufnr(bufselect)==matches[1]) ? 'c' : 'l')
endfunction

" User commands
command! -nargs=0 -bar Copen    bot copen 8 | pclose
command! -nargs=0 -bar Cclose   cclose
command! -nargs=0 -bar Ctoggle  if s:Qtype('')==?'c' | Cclose | else | Copen | endif
command! -nargs=0 -bar Cnext    call s:Wnext('c')
command! -nargs=0 -bar Cprev    call s:Wprev('c')
command! -nargs=+ -bar Csys     call s:Wsys('c',<f-args>)
command! -nargs=+ -bar Ccmd     call s:Wcmd('c',<f-args>)
command! -nargs=0 -bar Ccopy    call setqflist(getloclist(0))
command! -nargs=0 -bar Cclear   call setqflist([])

" Keymapping
nnoremap <localleader>c   :Ctoggle<CR>
nnoremap <silent>c        :Cnext<CR>
nnoremap <silent>C        :Cprev<CR>


" *******************************************************
" } Location window management {
" *******************************************************

" User commands
command! -nargs=0 -bar Lopen    bot lopen 8 | pclose
command! -nargs=0 -bar Lclose   lclose
command! -nargs=0 -bar Ltoggle  if s:Qtype('')==?'l' | Lclose | else | Lopen | endif
command! -nargs=0 -bar Lnext    call s:Wnext('l')
command! -nargs=0 -bar Lprev    call s:Wprev('l')
command! -nargs=+ -bar Lsys     call s:Wsys('l',<f-args>)
command! -nargs=+ -bar Lcmd     call s:Wcmd('l',<f-args>)
command! -nargs=0 -bar Lcopy    call setloclist(0,getqflist())
command! -nargs=0 -bar Lclear   call setloclist(0,[])

" Keymapping
nnoremap <localleader>l   :Ltoggle<CR>
nnoremap <silent>l        :Lnext<CR>
nnoremap <silent>L        :Lprev<CR>


" *******************************************************
" } Tags {
" *******************************************************
" Set tags root
let g:tags_db=['.tags']
"set tags=./.tags,.tags;$HOME
set tags=./.tags;$HOME

" User commands
command! -nargs=0 -bar Tnext    call s:Wnext('t')
command! -nargs=0 -bar Tprev    call s:Wprev('t')
command! -nargs=0 -bar Ttag     execute 'silent! ltag' expand('<cword>') | call setqflist(getloclist(0))

" Key mapping
noremap <C-ENTER>           :Ttag<CR>
noremap <C-BACKSPACE>       <C-t>
nnoremap <silent>t          :execute (&previewwindow ? 'P' : 'T').'next'<CR>
nnoremap <silent>T          :execute (&previewwindow ? 'P' : 'T').'prev'<CR>


" *******************************************************
" } Preview window {
" *******************************************************
" Options
set previewheight=12          " Preview window height

" Variables
let s:p_lastw = ""
let s:p_highlight = 0
let s:p_center = 0

" Open preview window
function! s:PreviewOpenWnd()
	silent! execute "bot pedit!"
	wincmd P
	if &previewwindow
		set nu
		"wincmd J
		wincmd p
		cclose | lclose
	endif
	augroup PreviewWnd
		au!
		au! CursorHold * nested call s:PreviewShowTag()
		"au! CursorHold * nested windo if &previewwindow|call s:PreviewShowTag()|endif
	augroup END
endfunction

" Close preview window
function! s:PreviewCloseWnd()
	augroup PreviewWnd
		au!
	augroup END
	pclose
endfunction

" Toggle preview window
function! s:PreviewToggleWnd()
	silent! wincmd P
	if &previewwindow
		wincmd p
		call s:PreviewCloseWnd()
	else
		call s:PreviewOpenWnd()
	endif
endfunction

function! s:PreviewShowTag()
	if &previewwindow             " don't do this in the preview window
		return
	endif

	for w in range(1, winnr('$')+1)	" Don't do this when preview window is not visible
		if getwinvar(w,'&previewwindow') == 1
			break
		endif
	endfor
	if w > winnr('$')
		return
	endif

	cclose | lclose               " Close quickfix & location windows

	let w = expand("<cword>")     " get the word under cursor
	if w == s:p_lastw             " Same word, skip all this
		return
	endif
	let s:p_lastw = w

	if w =~ '\a'                  " if the word contains a letter
		" Try displaying a matching tag for the word under the cursor
		try
			exec "silent! ptag " . w
			if has("folding")
				silent! .foldopen	" don't want a closed fold
			endif
			if s:p_highlight
				call s:PreviewHighlightTag(w)
			endif
			if s:p_center
				call s:PreviewCenterTag()
			endif
		endtry
	endif
endfunction

function! s:PreviewCenterTag()
	silent! wincmd P            " jump to preview window
	if &previewwindow           " if we really get there...
		normal! zz              " Center
		wincmd p                " back to old window
	endif
endfunction

function! s:PreviewHighlightTag(pattern)
	silent! wincmd P            " jump to preview window
	if &previewwindow           " if we really get there...
		match none              " delete existing highlight
		if has("folding")
			silent! .foldopen   " don't want a closed fold
		endif
		call search("$", "b")   " to end of previous line
		let w = substitute(a:pattern, '\\', '\\\\', "")
		call search('\<\V' . w . '\>') " position cursor on match
		" Add a match highlight to the word at this position
		hi previewWord term=bold ctermbg=blue guibg=blue
		exe 'match previewWord "\%' . line(".") . 'l\%' . col(".") . 'c\k*"'
		wincmd p                " back to old window
	endif
endfunction

" User commands
command! -nargs=0 -bar Popen    call s:PreviewOpenWnd()
command! -nargs=0 -bar Pclose   call s:PreviewCloseWnd()
command! -nargs=0 -bar Ptoggle  call s:PreviewToggleWnd()
command! -nargs=0 -bar Pnext    call s:Wnext('pt')
command! -nargs=0 -bar Pprev    call s:Wprev('pt')
command! -nargs=0 -bar Ptag     call s:PreviewShowTag()

" Key mapping
nmap <localleader>p             :Ptoggle<CR>


" *******************************************************
" } The end
" *******************************************************

